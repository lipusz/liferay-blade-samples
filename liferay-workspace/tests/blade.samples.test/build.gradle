sourceSets {
	testFunctional {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/testFunctional/java')
		}
		resources.srcDir file('src/testFunctional/resources')
	}
}

configurations {
	testFunctionalCompile.extendsFrom testCompile
	testFunctionalRuntime.extendsFrom testRuntime
}

dependencies {
	compileOnly "com.liferay.portal:com.liferay.portal.kernel:2.0.0"
	compileOnly "javax.portlet:portlet-api:2.0"
	compileOnly "javax.servlet:javax.servlet-api:3.0.1"
	compileOnly "org.osgi:org.osgi.service.component.annotations:1.3.0"

	testCompile group: "com.liferay.arquillian", name: "com.liferay.arquillian.arquillian-container-liferay", version: "1.0.6"
	testCompile group: "junit", name: "junit", version: "4.12"
	testCompile group: "org.jboss.arquillian.graphene", name: "graphene-webdriver", version: "2.1.0.Final"
	testCompile group: "org.jboss.arquillian.junit", name: "arquillian-junit-container", version: "1.1.11.Final"
}

startTestableTomcat {
	checkTimeout = 10 * 60 * 1000
}

task testFunctional(type: Test) {
	testClassesDir = sourceSets.testFunctional.output.classesDir
	classpath = sourceSets.testFunctional.runtimeClasspath
	outputs.upToDateWhen { false }

	dependsOn ":initBundle", startTestableTomcat
	dependsOn ":modules:blade.controlmenuentry:jar"
	dependsOn ":modules:blade.language:deploy"
	dependsOn ":modules:blade.language.web:jar"
	dependsOn ":modules:blade.portlet.actioncommand:jar"
	dependsOn ":modules:blade.portlet.configuration.icon:jar"
	dependsOn ":modules:blade.portlet.freemarker:jar"
	dependsOn ":modules:blade.portlet.jsp:jar"
	dependsOn ":modules:blade.portlet.osgiapi:jar"
	dependsOn ":modules:blade.portlet.rendercommand:jar"
	dependsOn ":modules:blade.portlet.toolbar.contributor:jar"

	systemProperty 'controlMenuEntryJarFile', project(':modules:blade.controlmenuentry').jar.archivePath
	systemProperty 'languageWebJarFile', project(':modules:blade.language.web').jar.archivePath
	systemProperty 'portletActionommandJarFile', project(':modules:blade.portlet.actioncommand').jar.archivePath
	systemProperty 'portletConfigurationIconJarFile', project(':modules:blade.portlet.configuration.icon').jar.archivePath
	systemProperty 'portletFreemarkerJarFile', project(':modules:blade.portlet.freemarker').jar.archivePath
	systemProperty 'portletJspJarFile', project(':modules:blade.portlet.jsp').jar.archivePath
	systemProperty 'portletOsgiApiJarFile', project(':modules:blade.portlet.osgiapi').jar.archivePath
	systemProperty 'portletRenderCommandJarFile', project(':modules:blade.portlet.rendercommand').jar.archivePath
	systemProperty 'portletToolbarContributorJarFile', project(':modules:blade.portlet.toolbar.contributor').jar.archivePath

	finalizedBy {
		stopTestableTomcat
	}

	doLast {
		delete new File("target/drone-phantomjs")
	}
}

stopTestableTomcat {
	doLast {
		delete new File(liferay.liferayHome, "data")
	}
}

check.dependsOn testFunctional